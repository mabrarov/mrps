if(NOT DEFINED LIBEV_PUBLIC_VAR_NS)
    set(LIBEV_PUBLIC_VAR_NS "Libev")
endif()

if(NOT DEFINED LIBEV_PRIVATE_VAR_NS)
    set(LIBEV_PRIVATE_VAR_NS "_${LIBEV_PUBLIC_VAR_NS}")
endif()

if(NOT DEFINED ${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME)
    set(${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME "Libev")
endif()

if(NOT DEFINED ${LIBEV_PRIVATE_VAR_NS}_BASE_NAME)
    set(${LIBEV_PRIVATE_VAR_NS}_BASE_NAME "ev")
endif()

if(${LIBEV_PUBLIC_VAR_NS}_USE_STATIC_LIBS)
    set(${LIBEV_PRIVATE_VAR_NS}_LIB_NAME_PREFIX "lib")
    set(${LIBEV_PRIVATE_VAR_NS}_LIB_NAME_SUFFIX "${CMAKE_STATIC_LIBRARY_SUFFIX}")
    set(${LIBEV_PRIVATE_VAR_NS}_LINKAGE_TYPE "STATIC")
else()
    set(${LIBEV_PRIVATE_VAR_NS}_LINKAGE_TYPE "")
endif()

list(APPEND ${LIBEV_PRIVATE_VAR_NS}_INCLUDE_SEARCH_DIRS ${${LIBEV_PUBLIC_VAR_NS}_ROOT})
list(APPEND ${LIBEV_PRIVATE_VAR_NS}_LIBRARY_SEARCH_DIRS ${${LIBEV_PUBLIC_VAR_NS}_ROOT})
if(${LIBEV_PUBLIC_VAR_NS}_NO_SYSTEM_PATHS)
    list(APPEND ${LIBEV_PRIVATE_VAR_NS}_INCLUDE_SEARCH_DIRS NO_CMAKE_SYSTEM_PATH NO_SYSTEM_ENVIRONMENT_PATH)
    list(APPEND ${LIBEV_PRIVATE_VAR_NS}_LIBRARY_SEARCH_DIRS NO_CMAKE_SYSTEM_PATH NO_SYSTEM_ENVIRONMENT_PATH)
endif()

find_path(${LIBEV_PUBLIC_VAR_NS}_INCLUDE_DIRS
    NAMES "${${LIBEV_PRIVATE_VAR_NS}_BASE_NAME}.h"
    HINTS ${${LIBEV_PRIVATE_VAR_NS}_INCLUDE_SEARCH_DIRS}
    PATH_SUFFIXES ${${LIBEV_PUBLIC_VAR_NS}_INCLUDE_SUFFIXES}
    DOC "Include directories for libev")

if(${LIBEV_PUBLIC_VAR_NS}_INCLUDE_DIRS)
    list(APPEND ${LIBEV_PRIVATE_VAR_NS}_POSSIBLE_RELEASE_NAMES
        "${${LIBEV_PRIVATE_VAR_NS}_LIB_NAME_PREFIX}${${LIBEV_PRIVATE_VAR_NS}_BASE_NAME}${${LIBEV_PRIVATE_VAR_NS}_LIB_NAME_SUFFIX}")
    list(APPEND ${LIBEV_PRIVATE_VAR_NS}_POSSIBLE_DEBUG_NAMES
        "${${LIBEV_PRIVATE_VAR_NS}_LIB_NAME_PREFIX}${${LIBEV_PRIVATE_VAR_NS}_BASE_NAME}d${${LIBEV_PRIVATE_VAR_NS}_LIB_NAME_SUFFIX}")

    find_library(${LIBEV_PRIVATE_VAR_NS}_LIB_RELEASE
        NAMES ${${LIBEV_PRIVATE_VAR_NS}_POSSIBLE_RELEASE_NAMES}
        HINTS ${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_SEARCH_DIRS}
        PATH_SUFFIXES ${${LIBEV_PUBLIC_VAR_NS}_LIB_SUFFIXES}
        DOC "Release libraries for libev")

    find_library(${LIBEV_PRIVATE_VAR_NS}_LIB_DEBUG
        NAMES ${${LIBEV_PRIVATE_VAR_NS}_POSSIBLE_DEBUG_NAMES}
        HINTS ${${LIBEV_PUBLIC_VAR_NS}_ROOT} ${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_SEARCH_DIRS}
        PATH_SUFFIXES ${${LIBEV_PUBLIC_VAR_NS}_LIB_SUFFIXES}
        DOC "Debug libraries for libev")

    if(${LIBEV_PRIVATE_VAR_NS}_LIB_RELEASE OR ${LIBEV_PRIVATE_VAR_NS}_LIB_DEBUG)
        add_library(${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME}
            ${${LIBEV_PRIVATE_VAR_NS}_LINKAGE_TYPE} IMPORTED)

        if(${LIBEV_PRIVATE_VAR_NS}_LIB_RELEASE)
            set_property(TARGET ${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME}
                APPEND PROPERTY IMPORTED_CONFIGURATIONS RELEASE)
            set_target_properties(${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME}
                PROPERTIES IMPORTED_LOCATION_RELEASE
                "${${LIBEV_PRIVATE_VAR_NS}_LIB_RELEASE}")
            list(APPEND ${LIBEV_PUBLIC_VAR_NS}_LIBRARIES
                optimized ${${LIBEV_PRIVATE_VAR_NS}_LIB_RELEASE})
        endif()

        if(${LIBEV_PRIVATE_VAR_NS}_LIB_DEBUG)
            set_property(TARGET ${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME}
                APPEND PROPERTY IMPORTED_CONFIGURATIONS DEBUG)
            set_target_properties(${${LIBEV_PRIVATE_VAR_NS}_LIBRARY_NAME}
                PROPERTIES IMPORTED_LOCATION_DEBUG
                "${${LIBEV_PRIVATE_VAR_NS}_LIB_DEBUG}")
            list(APPEND ${LIBEV_PUBLIC_VAR_NS}_LIBRARIES
                debug ${${LIBEV_PRIVATE_VAR_NS}_LIB_DEBUG})
        endif()
    endif()
endif()

include(FindPackageHandleStandardArgs)
if(${LIBEV_PUBLIC_VAR_NS}_FIND_REQUIRED AND NOT ${LIBEV_PUBLIC_VAR_NS}_FIND_QUIETLY)
    find_package_handle_standard_args(${LIBEV_PUBLIC_VAR_NS}
        REQUIRED_VARS ${LIBEV_PUBLIC_VAR_NS}_LIBRARIES ${LIBEV_PUBLIC_VAR_NS}_INCLUDE_DIRS
        FAIL_MESSAGE "libev not found")
else()
    find_package_handle_standard_args(${LIBEV_PUBLIC_VAR_NS}
        "libev not found"
        ${LIBEV_PUBLIC_VAR_NS}_LIBRARIES ${LIBEV_PUBLIC_VAR_NS}_INCLUDE_DIRS)
endif()

mark_as_advanced(${LIBEV_PUBLIC_VAR_NS}_INCLUDE_DIRS
    ${LIBEV_PUBLIC_VAR_NS}_LIBRARIES)
